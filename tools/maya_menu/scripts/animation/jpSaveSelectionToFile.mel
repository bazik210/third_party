//James Parks
//08-09-2004

//save selection procedure for jpAnimWriter

///////////////////////
//write a file that saves a selection set
///////////////////////
global proc jpSaveSelectionToFile(string $name, string $selected[])
{
	global string $aWriterVerNum;
	string $project = `workspace -q -fn`;
	string $currentFolder = jpGetCheckedFolder();

	if ( !`size($selected)` )
		error "Nothing is currently selected. No file created, or action taken.";

	string $safeFileCommand = "//James Parks\n//This selection script generated by jpAnimWriter v" + $aWriterVerNum + "\n\n";
	$safeFileCommand = $safeFileCommand + "\n";

	for ($node in $selected)
	{
		$safeFileCommand = $safeFileCommand + ("catch (`select -add \"" + $node + "\"`);\n");
	}
	
	string $selectionFolder = (`workspace -q -fn` + "\/data\/SELS\/" + $currentFolder + "/");
	string $selectionFileName = ($name + ".SEL");
	string $thisSelectionFile = ($selectionFolder + $selectionFileName);

	
	if(`menuItem -q -cb overwrite` == 1)
	{
		//check if the file exists already and if the user wants to overwrite it
		//then write the file and check again to make sure it exists
		if(`filetest -r $thisSelectionFile`)
		{
			int $pass = jpPasswordCheck();

				if($pass == 1)
				{
					string $confirm = `confirmDialog 
						-t "Overwrite File?" 
						-ma "center" 
						-m "This file already exists. Overwrite?" 
						-b "Yes"
						-b "No"
						-db "No"`
						;

					if($confirm == "Yes")
					{
						$curSelectionFile = `fopen $thisSelectionFile "w+"`;
						fprint $curSelectionFile $safeFileCommand;
						fclose $curSelectionFile;

						if(`filetest -r $thisSelectionFile`)
						{
							print ("\nPose saved to: " + $thisSelectionFile);
						}//end if
						else
						{
							warning "An error has occured while saving your file. Perhaps it's a permissions problem?";
						}//end else
					}//end if
				}//end if
				else
				{
					warning "Password Failed";
				}
		}//end if: overwrite filetest
		else
		{
			$curSelectionFile = `fopen $thisSelectionFile "w+"`;
			fprint $curSelectionFile $safeFileCommand;
			fclose $curSelectionFile;

			if(`filetest -r $thisSelectionFile`)
			{
				print ("\nSelection saved to: " + $thisSelectionFile);
			}//end if
			else
			{
				warning "An error has occured with saving your file. Perhaps it's a permissions problem?";
			}//end else
		}//end else
	}//end if
	else
	{
		$curSelectionFile = `fopen $thisSelectionFile "w+"`;
		fprint $curSelectionFile $safeFileCommand;
		fclose $curSelectionFile;

		if(`filetest -r $thisSelectionFile`)
		{
			print ("\nSelection saved to: " + $thisSelectionFile);
		}//end if
		else
		{
			warning "An error has occured with saving your file. Perhaps it's a permissions problem?";
		}//end else
	}//end else
	
	jpPopulateFileList();
}//end proc jpSaveSelectionToFile