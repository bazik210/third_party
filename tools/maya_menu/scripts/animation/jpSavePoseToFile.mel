//James Parks
//08-09-2004

//save pose procedure for jpAnimWriter

///////////////////////
//write a file that saves a pose
///////////////////////
global proc jpSavePoseToFile(string $name, string $selected[], string $keyableAttrs[])
{
	global string $aWriterVerNum;
	string $project = `workspace -q -fn`;
	string $currentFolder = jpGetCheckedFolder();

	int $reCreateKeyableAttrs = 0;

	if ( !`size($selected)` )
		error "Nothing is currently selected. No file created, or action taken.";

	string $safeFileCommand = "//James Parks\n//This pose script generated by jpAnimWriter v" + $aWriterVerNum + "\n\n";

	for ($node in $selected)
	{
		$strippedNode = jpHandleReference_write($node);

		$safeFileCommand = ($safeFileCommand + "\n//Values for " + $strippedNode + "\n$object = \"" + $strippedNode + "\";\n");
		$safeFileCommand = ($safeFileCommand + "$object = jpHandleReference_read($object);\n\n");

		if($keyableAttrs[0] == "" || $reCreateKeyableAttrs == 1)
		{
			$keyableAttrs = `listAttr -r -w -k -u -v -m -s $node`;
			$reCreateKeyableAttrs = 1;
		}//end if

		for ($attr in $keyableAttrs)
		{
			string $value = string ( `getAttr ($node+"."+$attr)` );
			$safeFileCommand = $safeFileCommand + ("catch (`setAttr ($object + \"" +"."+$attr+"\") "+$value+"`);\n");
		}
	}

	string $posesFolder = (`workspace -q -fn` + "\/data\/POSES\/" + $currentFolder + "/");
	string $poseFileName = ($name + ".POSE");
	string $thisPoseFile = ($posesFolder + $poseFileName);


		if(`menuItem -q -cb overwrite` == 1)
		{
			//check if the file exists already and if the user wants to overwrite it
			//then write the file and check again to make sure it exists
			if(`filetest -r $thisPoseFile`)
			{
				int $pass = jpPasswordCheck();

				if($pass == 1)
				{
					string $confirm = `confirmDialog 
						-t "Overwrite File?" 
						-ma "center" 
						-m "This file already exists. Overwrite?" 
						-b "Yes"
						-b "No"
						-db "No"`
						;

					if($confirm == "Yes")
					{
						$curPoseFile = `fopen $thisPoseFile "w+"`;
						fprint $curPoseFile $safeFileCommand;
						fclose $curPoseFile;

						if(`filetest -r $thisPoseFile`)
						{
							print ("\nPose saved to: " + $thisPoseFile);
						}//end if
						else
						{
							warning "An error has occured while saving your file. Perhaps it's a permissions problem?";
						}//end else
					}//end if
				}//end if
				else
				{
					warning "Password Failed";
				}
			}//end if
			else
			{
				$curPoseFile = `fopen $thisPoseFile "w+"`;
				fprint $curPoseFile $safeFileCommand;
				fclose $curPoseFile;

				if(`filetest -r $thisPoseFile`)
				{
					print ("\nPose saved to: " + $thisPoseFile);
				}//end if
				else
				{
					warning "An error has occured while saving your file. Perhaps it's a permissions problem?";
				}//end else
			}//end else
		}//end if
		else
		{
			$curPoseFile = `fopen $thisPoseFile "w+"`;
			fprint $curPoseFile $safeFileCommand;
			fclose $curPoseFile;

			if(`filetest -r $thisPoseFile`)
			{
				print ("\nPose saved to: " + $thisPoseFile);
			}//end if
			else
			{
				warning "An error has occured while saving your file. Perhaps it's a permissions problem?";
			}//end else
		}//end else
	
	jpPopulateFileList();
}//end proc jpSavePoseToFile